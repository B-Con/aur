# Maintainer: Brad Conte <brad AT bradconte.com>
pkgname=ntl
pkgver=6.0.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=('i686' 'x86_64')
url="http://www.shoup.net/ntl/"
license=('GPL')
depends=('gf2x' 'gmp')
options=('!libtool')
source=("http://www.shoup.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9342fa7d83a0ee6748ebae683368250eff06b19d7df5dfb85bcba8a896c0a29b')

build() {
	cd "$srcdir/$pkgname-$pkgver/src"
	./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NTL_GMP_LIP=on || return 1
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver/src"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver/src"
	make PREFIX="$pkgdir/usr" install
}
